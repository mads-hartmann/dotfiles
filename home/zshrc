# .zshrc is sourced in interactive shells. It should contain
# commands to set up aliases, functions, options, key bindings, etc.

### Register lazily loaded functions.
#
#    This tells zsh that these functions exist but that it shouldn't
#    load the body of the functions before they're used. The value of
#    $fpath is used to find the bodies.

fpath=(~/.zsh-functions $fpath)

# Built-in functions
autoload -U colors && colors # force-load.

# Custom functions
for function_file (~/.zsh-functions/*); do
  autoload -Uz ${function_file:t} # :t takes just the name.
done

### Oh-my-zsh

plugins=(git docker)
export ZSH=$HOME/.oh-my-zsh
export ZSH_THEME=""
source $ZSH/oh-my-zsh.sh

### Aliases.
#
#    various commands that I'm too lazy to type out all the time.

alias pj="select-project"
alias f="famlydev"
alias epoch="date +%s"
alias e="emacsclient -nw --socket-name=shell-emacs"
alias eg="/Applications/Emacs.app/Contents/MacOS/bin/emacsclient -c --no-wait"

# Starts an emacs daemon with a dedicated name. I use this daemon
# whenever I use emacs from the terminal.
alias emacs-daemon="emacs --daemon=shell-emacs"

alias dps="docker ps --format 'table {{.ID}}\t{{.Names}}\t{{.Ports}}\t{{.Status}}'"
alias dc="docker-compose"

### Keybindings.
#
# This binds keys to Zle widgets (Zsh Line Editor widgets.).
# To get a list of all widgets run zle -la
#
# See this link for more information.
#
#   http://sgeb.io/posts/2014/04/zsh-zle-custom-widgets/
#   http://zsh.sourceforge.net/Doc/Release/Zsh-Line-Editor.html#Standard-Widgets
#
# You can use `bindkey 'M-x'` to see what command M-x is bound to. Use `bindkey`
# to get a list of all mappings.
#
# ^[ is alt.
#

bindkey '^[l' down-case-word
bindkey '^[u' up-case-word
bindkey -s '^[p' '^Uselect-project^M' # Ctrl+u to delete line, select-project, enter.
bindkey -s '^[s' '^Ugit-switch^M'

### Prompt.

function promt_part_git {
  local git_branch

  git rev-parse --is-inside-work-tree &> /dev/null

  if [[ $status == 0 ]]
  then
    git_branch=$(git rev-parse --abbrev-ref HEAD)
    echo " on %{$fg[green]%}$git_branch%{$reset_color%}"
  fi
}

PROMPT='%{$fg[magenta]%}%~%{$reset_color%}$(promt_part_git) '
